import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.internal.jvm.Jvm

import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.runtime' version '1.12.5'
    id "name.remal.check-updates" version "1.3.1"

    // TODO no good alternative to maven duplicate finder
    // id "net.idlestate.gradle-duplicate-classes-check" version "1.0.2"
}

version = readEnvOrDefault 'VERSION', '0.DEV'
sourceCompatibility = '14'

ext {
    javafxVersion = '16'
    windowsVmPath = "$System.env.HOME/Virtual Machines.localized/Windows 10 x64.vmwarevm/Windows 10 x64.vmx"
    linuxVmPath = "$System.env.HOME/Virtual Machines.localized/OpenSUSE.vmwarevm/OpenSUSE.vmx"
    fullPackageOutputDir = "$buildDir/fullpackage"
}

repositories {
    mavenLocal() {
        content {
            includeGroup "net.yudichev.jiotty"
        }
    }
    mavenCentral()
    maven {
        url "https://maven.google.com"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
    maven {
        url "https://sandec.jfrog.io/artifactory/repo"
    }
}

dependencies {
    ext.orgJunitJupiterVersion = '5.7.2'
    ext.orgMockitoVersion = '3.10.0'
    ext.orgImmutablesVersion = '2.8.8'
    ext.netYudichevJiottyVersion = '2.0.3-SNAPSHOT'
    ext.orgApacheLoggingLog4jVersion = '2.14.1'

    annotationProcessor "org.immutables:value:$orgImmutablesVersion"
    testAnnotationProcessor "org.immutables:value:$orgImmutablesVersion"

    implementation platform("net.yudichev.jiotty:jiotty-bom:$netYudichevJiottyVersion")
    implementation enforcedPlatform(project(':platform'))

    implementation "net.yudichev.jiotty:jiotty-common"
    implementation "net.yudichev.jiotty:jiotty-connector-google-common"
    implementation "net.yudichev.jiotty:jiotty-connector-google-photos"
    implementation "net.yudichev.jiotty:jiotty-connector-google-drive"
    implementation "com.google.inject.extensions:guice-assistedinject"
    implementation "org.slf4j:slf4j-api"
    implementation "org.apache.logging.log4j:log4j-api"
    implementation "com.google.inject:guice"
    implementation "javax.inject:javax.inject"
    implementation "com.google.guava:guava:29.0-jre"
    implementation "com.google.code.findbugs:jsr305"
    implementation "com.google.errorprone:error_prone_annotations"
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.google.api:gax"
    implementation "com.google.api.grpc:proto-google-common-protos"
    implementation "commons-cli:commons-cli:1.4"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "de.codecentric.centerdevice:centerdevice-nsmenufx:2.1.7"
    implementation "io.grpc:grpc-api"
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.sandec:mdfx:0.2.3"
    implementation 'com.h2database:h2:1.4.200'

    // This is to test dependency analyser
    //implementation "org.apache.commons:commons-skin:4.2"
    compileOnly "org.immutables:value:$orgImmutablesVersion"
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$orgApacheLoggingLog4jVersion"
    runtimeOnly "org.apache.logging.log4j:log4j-jcl:$orgApacheLoggingLog4jVersion"
    runtimeOnly "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.3"
    runtimeOnly "com.lmax:disruptor:3.4.4"

    testImplementation "net.yudichev.jiotty:jiotty-common"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$orgJunitJupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$orgJunitJupiterVersion"
    testImplementation "net.yudichev.jiotty:jiotty-connector-google-drive::tests"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$orgJunitJupiterVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$orgMockitoVersion"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testCompileOnly "org.immutables:value:$orgImmutablesVersion"
}

test {
    systemProperty "user.language", "en"
}

String clientSecretPath = readEnvOrNull('CLIENT_SECRET_PATH')
task copyClientSecretIfDefined {
    doLast {
        if (clientSecretPath) {
            if (!file(clientSecretPath).exists()) {
                throw new InvalidUserDataException("Client secret file not found: $clientSecretPath")
            }
            processResources {
                from clientSecretPath
                rename(file(clientSecretPath).name, 'client_secret.json')
            }
        }
    }
}
processResources.dependsOn 'copyClientSecretIfDefined'

task requireClientSecret {
    doLast {
        if (!clientSecretPath) {
            throw new InvalidUserDataException("CLIENT_SECRET_PATH system property or environment variable missing - " +
                    "should exist and contain a path to Google OAuth client secret file")
        }
    }
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': archiveVersion
    }
}

application {
    mainClassName = 'net.yudichev.googlephotosupload.Main'
    applicationName = 'Jiotty Photos Uploader'
    applicationDefaultJvmArgs = ['--add-exports', 'javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED',
                                 '--add-opens', 'javafx.graphics/com.sun.javafx.tk.quantum=ALL-UNNAMED',
                                 '--add-opens', 'javafx.controls/javafx.scene.control=ALL-UNNAMED',
                                 '--add-exports', 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
                                 '--add-exports', 'javafx.controls/com.sun.javafx.scene.control=ALL-UNNAMED',
                                 '--add-exports', 'javafx.graphics/com.sun.javafx.menu=ALL-UNNAMED']
}

javafx {
    version = "$javafxVersion"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
    if (project.hasProperty("raspberryPi")) {
        // do not include native libraries on RPi
        configuration = 'compileOnly'
    }
}

runtime {
    //noinspection GroovyAssignabilityCheck, GroovyAccessibility, GrFinalVariableAccess
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages']

    // required by httpclient, missed by default when building and running on Windows
    //noinspection GroovyAssignabilityCheck,GroovyAccessibility,GrFinalVariableAccess
    modules = [
            // required by httpclient, missed by default when building and running on Windows
            'java.naming',
            // required to establish an SSL connection
            'jdk.crypto.ec',
            // required for a heap dump via JMX
            'jdk.management'
    ]
    //noinspection GroovyAssignabilityCheck,GroovyAccessibility,GrFinalVariableAccess
    additive = true

    jpackage {
        imageName = 'Jiotty Photos Uploader'
        installerName = 'Jiotty Photos Uploader'
        jvmArgs = ['-Dprism.verbose=true']
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            installerType = 'dmg'
            imageOptions = ['--verbose', /*'--mac-sign',*/ '--mac-signing-key-user-name', 'Alexey Yudichev (J4R72JZQ9P)']
            installerOptions = ['--verbose']
            resourceDir = file("$buildDir/packaging-resources/macOS/out")
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = 'msi'
            imageOptions = ['--verbose']
            installerOptions = ['--verbose', '--win-per-user-install', '--win-menu', '--win-upgrade-uuid', '58e3cc92-0df9-4089-9609-8ad54b2608e7']
            resourceDir = file("$projectDir/src/main/packaging-resources/windows")
        }
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            if (project.hasProperty("raspberryPi")) {
                // do not try to build rpm on RPi
                installerType = 'deb'
            }
            imageName = 'JiottyPhotosUploader'  // cannot have spaces in Debian resource names
            installerName = 'JiottyPhotosUploader' // cannot have spaces in Debian resource names
            imageOptions = ['--verbose']
            resourceDir = file("$projectDir/src/main/packaging-resources/linux")
            installerOptions = ['--verbose', '--linux-shortcut', '--linux-package-name', 'jiotty-photos-uploader']
        }
    }
}
jpackage.dependsOn 'check'
jpackage.dependsOn 'requireClientSecret'

task copyPackagingResources(type: Copy) {
    onlyIf { !org.gradle.internal.os.OperatingSystem.current().linux }
    if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
        from("$projectDir/src/main/packaging-resources/macOS/out") {
            exclude '**/*.plist'
        }
        from("$projectDir/src/main/packaging-resources/macOS/out") {
            include '**/*.plist'
            filter(ReplaceTokens, tokens: [version: version])
        }
        into "$buildDir/packaging-resources/macOS/out"
    } else if (org.gradle.internal.os.OperatingSystem.current().windows) {
        from("$projectDir/src/main/packaging-resources/windows/out") {
            include '**/*.xml'
            filter(ReplaceTokens, tokens: [version: version, appName: project.name])
        }
        into "$buildDir/packaging-resources/windows/out"
    } else {
        // workaround for "task.destinationDir must not be null"
        destinationDir buildDir
    }
}
jpackage.dependsOn 'copyPackagingResources'

task ensureVersionReleaseReady {
    doLast {
        if (!(version ==~ /\d+\.\d+\.\d+/)) {
            throw new InvalidUserDataException("version must be set to X.Y.Z format but was: ${version}; add -DVERSION=X.Y.Z to the command line and try again")
        }
    }
}

def finalFileNameWindowsMsi = "Windows7-jiotty-photos-uploader-${version}.msi"
// TODO this file name is duplicated
def finalFileNameWindowsMsix = "Windows10-jiotty-photos-uploader-${version}.msix"

task prepareOsSpecificPackaging {
    onlyIf { org.gradle.internal.os.OperatingSystem.current().windows }
    doLast {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            ant.move file: "$buildDir/jpackage/$applicationName-${version}.msi",
                    tofile: "$fullPackageOutputDir/$finalFileNameWindowsMsi"
        }
    }
}

task runOsSpecificPackaging(type: Exec) {
    onlyIf { !org.gradle.internal.os.OperatingSystem.current().linux }
    if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
        commandLine "$rootDir/macos_notarize_dmg.sh", "$project.name", "$version", Jvm.current().getJavaHome()
    }
    if (org.gradle.internal.os.OperatingSystem.current().windows) {
        commandLine "$rootDir/windows-create-appx.bat", "$version"
    }
}

task renameDistribution() {
    onlyIf { !org.gradle.internal.os.OperatingSystem.current().windows }
    doLast {
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            ant.move file: "$buildDir/jpackage/$applicationName-${version}.dmg",
                    tofile: "$fullPackageOutputDir/macOS-jiotty-photos-uploader-${version}.dmg"
        }
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            ant.move(todir: fullPackageOutputDir) {
                fileset dir: "$buildDir/jpackage", includes: '*.deb,*.rpm'
                mapper type: "glob", from : "*", to: "Linux-*"
            }
        }
    }
}

/**
 * Package the app on the current platform.
 */
task fullpackage {
    doFirst {
        mkdir fullPackageOutputDir
    }

    dependsOn ensureVersionReleaseReady

    dependsOn jpackage
    jpackage.mustRunAfter 'ensureVersionReleaseReady'

    dependsOn prepareOsSpecificPackaging
    prepareOsSpecificPackaging.mustRunAfter 'jpackage'

    dependsOn runOsSpecificPackaging
    runOsSpecificPackaging.mustRunAfter 'prepareOsSpecificPackaging'

    dependsOn renameDistribution
    renameDistribution.mustRunAfter 'runOsSpecificPackaging'
}

int vmrun(GString vmPath, String usernamePropertyName, String pwdPropertyName, String command, List<String> args = [], ignoreExitCode = false) {
    def vmUser = readEnvOrFail usernamePropertyName
    def vmPassword = readEnvOrFail pwdPropertyName
    def argsFull = ['vmrun', '-T', 'ws', '-gu', vmUser, '-gp', vmPassword, command, vmPath] + args
    return exec {
        commandLine argsFull
        ignoreExitValue ignoreExitCode
    }.exitValue
}

int vmrunWindows(String command, List<String> args = [], ignoreExitCode = false) {
    vmrun windowsVmPath, 'VMRUN_WIN_USER', 'VMRUN_WIN_PWD', command, args, ignoreExitCode
}

task runRemoteWindowsBuild(dependsOn: requireClientSecret) {
    doLast {
        def remoteCloneDir = "C:\\java\\jiotty-photos-uploader"
        vmrunWindows 'start'
        def tmpClientSecretGuestPath = 'C:\\java\\clientSecret.json'
        vmrunWindows 'CopyFileFromHostToGuest', [clientSecretPath, tmpClientSecretGuestPath]
        mkdir "$buildDir/jpackage"
        def pullAndBuildExitCode = vmrunWindows 'runProgramInGuest', ['-interactive', '-activeWindow', "$remoteCloneDir\\pull_and_build.bat", "$version"], true
        vmrunWindows 'deleteFileInGuest', [tmpClientSecretGuestPath]
        def buildLogGuestPath = "$buildDir/pull_and_build.windows.log"
        vmrunWindows 'CopyFileFromGuestToHost', ["$remoteCloneDir\\pull_and_build.log", buildLogGuestPath], true
        def remoteLogFile = Paths.get(buildLogGuestPath)
        if (pullAndBuildExitCode != 0) {
            if (Files.exists(remoteLogFile)) {
                println Files.readString(remoteLogFile)
            }
            throw new GradleScriptException("VM build execution failed, see errors above", null)
        }
        mkdir fullPackageOutputDir
        vmrunWindows 'CopyFileFromGuestToHost',
                ["$remoteCloneDir\\build\\fullpackage\\$finalFileNameWindowsMsi", "$fullPackageOutputDir/$finalFileNameWindowsMsi"]
        vmrunWindows 'CopyFileFromGuestToHost',
                ["$remoteCloneDir\\build\\fullpackage\\$finalFileNameWindowsMsix", "$fullPackageOutputDir/$finalFileNameWindowsMsix"]
        vmrunWindows 'suspend'
    }
}

int vmrunLinux(String command, List<String> args = [], ignoreExitCode = false) {
    vmrun linuxVmPath, 'VMRUN_LINUX_USER', 'VMRUN_LINUX_PWD', command, args, ignoreExitCode
}

task runRemoteLinuxBuild(dependsOn: requireClientSecret) {
    doLast {
        def remoteCloneDir = '/home/alexey/java/jiotty-photos-uploader'
        def vmUser = readEnvOrFail 'VMRUN_LINUX_USER'
        vmrunLinux 'start'
        def tmpClientSecretGuestPath = "/home/$vmUser/java/clientSecret.json"
        vmrunLinux 'CopyFileFromHostToGuest', [clientSecretPath, tmpClientSecretGuestPath]
        mkdir "$buildDir/jpackage"
        vmrunLinux 'runScriptInGuest', ['/bin/bash', "chmod +x $remoteCloneDir/pull_and_build.sh"]
        def pullAndBuildExitCode = vmrunLinux 'runProgramInGuest', ['-interactive', '-activeWindow', "$remoteCloneDir/pull_and_build.sh", "$version"], true
        vmrunLinux 'deleteFileInGuest', [tmpClientSecretGuestPath]
        def buildLogPath = "$buildDir/pull_and_build.linux.log"
        vmrunLinux 'CopyFileFromGuestToHost', ["$remoteCloneDir/pull_and_build.log", buildLogPath], true
        def remoteLogFile = Paths.get(buildLogPath)
        if (pullAndBuildExitCode != 0) {
            if (Files.exists(remoteLogFile)) {
                println Files.readString(remoteLogFile)
            }
            throw new GradleScriptException("VM build execution failed, see errors above", null)
        }
        mkdir fullPackageOutputDir
        def finalFileNameLinuxDeb = "Linux-jiotty-photos-uploader_$version-1_amd64.deb"
        def finalFileNameLinuxRpm = "Linux-jiotty-photos-uploader-$version-1.x86_64.rpm"
        vmrunLinux 'CopyFileFromGuestToHost', ["$remoteCloneDir/build/fullpackage/$finalFileNameLinuxDeb", "$fullPackageOutputDir/$finalFileNameLinuxDeb"]
        vmrunLinux 'CopyFileFromGuestToHost', ["$remoteCloneDir/build/fullpackage/$finalFileNameLinuxRpm", "$fullPackageOutputDir/$finalFileNameLinuxRpm"]
        vmrunLinux 'suspend'
    }
}

task moveFinalArtifactsToDeploymentDir() {
    doLast {
        def destinationRootPath = Paths.get(readEnvOrFail('HYPERPACKAGE_DESTINATION_DIR'))
        def destinationVersionPath = destinationRootPath.resolve version
        mkdir destinationVersionPath
        ant.move file: "$fullPackageOutputDir",
                tofile: "$destinationVersionPath"
    }
}
/**
 * Package the app on all platforms.
 */
task hyperpackage {
    dependsOn fullpackage
    dependsOn runRemoteWindowsBuild
    dependsOn runRemoteLinuxBuild
    dependsOn moveFinalArtifactsToDeploymentDir
    moveFinalArtifactsToDeploymentDir.mustRunAfter 'fullpackage', 'runRemoteWindowsBuild', 'runRemoteLinuxBuild'
}

private static readEnvOrNull(String variableName) {
    System.getProperty(variableName, System.getenv(variableName))
}

private static readEnvOrFail(String variableName) {
    def value = readEnvOrNull(variableName)
    if (value == null) {
        throw new InvalidUserDataException("Missing environment or system property ${variableName}")
    }
    return value
}

private static readEnvOrDefault(String variableName, String defaultValue) {
    readEnvOrNull(variableName) ?: defaultValue
}